# This file contains most basic commands that are available to all users

command /afk:
	cooldown: 5 seconds
	cooldown message: &c&l (!) &cPlease wait before toggling your AFK status.
	trigger:
		if {game::%player%} is not "Spawn" or "AFK":
			sendError(player, "You must be at spawn to become AFK")
			exit
		if {game::%player%} is "AFK":
			message "&f%player% &7ɪѕ ɴᴏ ʟᴏɴɢᴇʀ ᴀꜰᴋ." to all players
			set {game::%player%} to "Spawn"
			spawn(player)
			exit
		message "&f%player% &7ɪѕ ɴᴏᴡ ᴀꜰᴋ" to all players
		clear inventory of player
		updateTablistName(player)
		send title "You are &6&lAFK" with subtitle "ʀᴜɴ &6/ᴀꜰᴋ &fᴀɢᴀɪɴ ᴛᴏ ɢᴏ ʙᴀᴄᴋ." for 1000 seconds with fadein 10 ticks and fadeout 1000 ticks
		send action bar "&fYou will be kicked in 20 minutes." to player
		message "%nl% &6&lAFK Kick Notice%nl% &fYou will be kicked if you AFK for over 20 minutes.%nl%"
		set {game::%player%} to "AFK"
		refreshScoreboard()
		updateTablistName(player)
		teleport player to location(0.5, 67, 0.5, "Shootout", 0, 90)
		hide player from all players
		loop 1200 times: # 20 minutes in seconds
			exit if {game::%player%} is not "AFK"
			wait 1 second
		send title "&cAFK Kick Warning!" with subtitle "&fᴜѕᴇ &6/afk &fᴛᴏ ʀᴇᴛᴜʀɴ!" for 60 seconds with fadein 0 ticks and fadeout 10 ticks
		message "%nl% &c&lAFK KICK WARNING%nl% &fYou have been AFK for 20 minutes! You will be kicked if you do not return to allow other players to join.%nl%"
		loop 4 times:
			play sound "block.note_block.didgeridoo" with pitch 0.9 to player
			wait 2 tick
			play sound "block.note_block.didgeridoo" with pitch 0.8 to player
			wait 4 ticks
		
		loop 30 times: # 30 seconds
			exit if {game::%player%} is not "AFK"
			wait 1 second
		send title "&c&lAFK Kick" with subtitle "&fᴜѕᴇ &6/afk &fᴛᴏ ʀᴇᴛᴜʀɴ!" for 60 seconds with fadein 0 ticks and fadeout 10 ticks
		message "%nl% &c&lAFK KICK%nl% &fYou are about to get kicked. Use &n/afk&f to return.%nl%"
		loop 4 times:
			play sound "block.note_block.didgeridoo" with pitch 0.8 to player
			wait 2 tick
			play sound "block.note_block.didgeridoo" with pitch 0.7 to player
			wait 4 ticks
		set {_time} to 5
		loop 5 times: # 5 seconds
			exit if {game::%player%} is not "AFK"
			wait 1 second
		loop 5 times:
			set {_s} to "ѕ" if {_time} is not 1
			set {_s} to "" if {_time} is 1
			send title "&c&lAFK KICK" with subtitle "&fʏᴏᴜ ᴡɪʟʟ ʙᴇ ᴋɪᴄᴋᴇᴅ ɪɴ %{_time}% ѕᴇᴄᴏɴᴅ%{_s}%!" for 2 seconds with fadein 0 ticks
			play sound "block.note_block.didgeridoo" with pitch 0.6 to player
			wait 1 second
			remove 1 from {_time}
			exit if {game::%player%} is not "AFK"
		
		kick player due to "&cYou went AFK for too long!"

command /discord:
	description: Gets the link to our Discord server.
	trigger:
		set {_t} to text component from "%nl%&3 > Join Discord Server%nl%"
		set hover event of {_t} to a new hover event showing "&fClick me to join the Permafrost community!"
		set click event of {_t} to a new click event to open url "https://discord.gg/mmhevxcFnM"
		play sound "entity.illusioner.cast_spell" to player
		message "%nl%&9&l DISCORD SERVER"
		message "&f Join our friendly Discord community for updates, sneak peeks, events, and more. Plus, check out our other games!"
		send component {_t} to player

command /ping:
	aliases: tps
	trigger:
		message ""
		# Give ping 
		if player's ping is less than 100:
			message "&lYour Ping: &a%player's ping%ms &o(Great!)"
		else if player's ping is between 100 and 150:
			message "&lYour Ping: &2%player's ping%ms &o(Good)"
		else if player's ping is between 150 and 200:
			message "&lYour Ping: &e%player's ping%ms &o(Slow)"
		else if player's ping is above 200:
			message "&lYour Ping: &c%player's ping%ms &o(Poor)"
			
		# Give TPS
		if tps from last 1 minute is greater than 19.99:
			message "&lServer TPS: &a20.00 &o(Perfect!)"
		else if tps from last 1 minute is greater than 19.98:
			message "&lServer TPS: &a%tps from last 1 minute% &o(Great)"
		else if tps from last 1 minute is between 19.98 and 19.90:
			message "&lServer TPS: &2%tps from last 1 minute% &o(Good)"
		else if tps from last 1 minute is between 19.90 and 19.60:
			message "&lServer TPS: &e%tps from last 1 minute% &o(Slow)"
		else if tps from last 1 minute is below 19.60:
			message "&lServer TPS: &c%tps from last 1 minute% &o(Poor)"
		message ""
			
		# Give conclusion
		if player's ping is less than 150:	
			if tps from last 1 minute is greater than 19.95:
				message "&aYou shouldn't experience any lag on the server. Your ping and the server TPS are very good."
				exit
				
		if player's ping is greater than 150:
			if tps from last 1 minute is greater than 19.95:
				message "&eYour ping is higher than the acceptable standard, but it looks like there isn't any lag on the server. You are likely to experience some lag."
				exit
				
		if tps from last 1 minute is less than 19.95:
			message "&cThe server seems to be laggy. If this lag continues, please notify a staff member."
			exit
				